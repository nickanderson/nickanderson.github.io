#+Title: Are CFEgine classes automatically canonified?
#+AUTHOR: Nick Anderson
#+DATE: 2017-11-20
#+TAGS[]: cfengine
#+DRAFT: false

#+BEGIN_QUOTE
  Is there implicit "canonify"-cation happening in in "classes:" promise type?
#+END_QUOTE

Yes the agent will automatically canonify classes that you define for your
convenience.

#+BEGIN_SRC cfengine3 :exports both :results output
  bundle agent main
  # @brief show how classes are automatically canonified during definition
  {
    vars:
      "invalid_class_string" string => "my-invalid-class";

    classes:

  @if minimum_version(3.10)
        # Shorthand for "myclass" expression => "any";
        # Available since 3.10.0
        "$(invalid_class_string)-1";
  @endif

        "$(invalid_class_string)-2" expression => "any";

    commands:

        "/bin/true"
          classes => results("bundle", "my-invalid-class-from-promise-result");

    vars:
        "c" slist => classesmatching("my.*");

    reports:
        "CFEngine $(sys.cf_version)";
        "Found defined class $(c)";
  }

#+END_SRC

#+RESULTS:
: R: CFEngine 3.11.0
: R: Found defined class my_invalid_class_2
: R: Found defined class my_invalid_class_1
: R: Found defined class my_invalid_class_from_promise_result_repaired
: R: Found defined class my_invalid_class_from_promise_result_reached

However, it will NOT automatically canonify strings that are being checked as
you can see in this example. You must explicitly canonify when checking.

#+BEGIN_SRC cfengine3 :exports both
  bundle agent main
  {
    vars:
        "my_invalid_class" string => "my-invalid-class";

    classes:
        "$(my_invalid_class)" expression => "any";

    reports:
        "The string $(my_invalid_class) is a valid class"
          if => $(my_invalid_class);

        "The string $(my_invalid_class) is NOT a valid class"
          if => not( $(my_invalid_class) );
      
        "The string $(my_invalid_class) when canonified is a valid class"
          if => classify( $(my_invalid_class) );
  }
#+END_SRC

#+RESULTS:
: R: The string my-invalid-class is NOT a valid class
: R: The string my-invalid-class when canonified is a valid class
