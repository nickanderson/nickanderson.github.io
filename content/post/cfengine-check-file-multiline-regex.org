#+Title: Check if a file contains a multi-line regex
#+AUTHOR: Nick Anderson
#+DATE: 2017-12-22T12:51:40-06:00
#+TAGS[]: cfengine
#+DRAFT: false

#+BEGIN_QUOTE
  Is there any way to do a multiline regex check for a file in CFEngine?
#+END_QUOTE

There is no function for searching a file for content. The [[https://docs.cfengine.com/docs/master/reference-functions-grep.html][=grep()= function]]
operates on lists, not files. However using [[https://docs.cfengine.com/docs/master/reference-functions-readfile.html][=readfile()=]] and [[https://docs.cfengine.com/docs/master/reference-functions-regcmp.html][=regcmp()=]] I was
sill able to search for a multi-line string using only native functionaity

In the example below we first create a file with content that spans multiple
lines. Then we read that file content into a variable and use a regular
expression on the variable content.

#+Name: Example showing how to search for a mutliline pattern in a file using native functions
#+Caption: Example showing how to search for a mutliline pattern in a file using native functions
#+BEGIN_SRC cfengine3
  bundle agent main
  {
        # CFEngine
        # is the agent you're looking for.

    files:
      "/tmp/example.txt"
        create => "true",
        edit_line => insert_lines( "CFEngine$(const.n)is the agent you're looking for" );

    vars:

        "file_content"
          string => readfile( "/tmp/example.txt" , inf ),
          if => not( isvariable( $(this.promiser) ) );

    classes:

        # Search for a multi-line string
        "I_found_the_agent_i_am_looking_for"
          expression => regcmp( "CFEngine\Ris\sthe\sagent.*",
                                $(file_content));

        "I_did_not_find_droids"
          expression => not( regcmp( ".*droid.*",
                                $(file_content)));


    reports:

        "CFEngine $(sys.cf_version)";

        "/tmp/example.txt contains:$(const.n)$(file_content)";

        "I found the agent i'm looking for."
          if => "I_found_the_agent_i_am_looking_for";

        "I see no droids."
          if => "I_did_not_find_droids";
  }
#+END_SRC

Results in this output:

#+RESULTS: Example showing how to search for a mutliline pattern in a file using native functions
: R: CFEngine 3.11.0
: R: /tmp/example.txt contains:
: CFEngine
: is the agent you're looking for
:
: R: I found the agent i'm looking for.
: R: I see no droids.

You may also want to check out this post about [[https://watson-wilson.ca/blog/2015/08/20/build-better-regular-expressions-in-cfengine/][building better regular
expressions in CFEngine]] from [[https://www.linkedin.com/in/neilhwatson/][Neil Watson]].
